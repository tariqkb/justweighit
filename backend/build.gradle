
buildscript {
    ext.kotlin_version = '1.0.3'

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.hidetake:gradle-ssh-plugin:2.7.0'
    }
}

plugins {
    id 'org.hidetake.ssh' version '2.7.0'
}

apply plugin: 'application'
apply plugin: 'kotlin'
apply plugin: 'distribution'
apply plugin: 'org.hidetake.ssh'

distributions {
    main {
        baseName = 'justweighit'
        contents {
            from ('src/main/resources/config.yml')
            from ('../frontend/dist') {
                into 'frontend'
            }
        }
    }
}

distTar {
    compression = Compression.GZIP
}

remotes {
    prod {
        host = 'justweighit.com'
        user = 'jwi'
        identity = file(project.property('SSH_KEY_FILE'))
        fileTransfer = 'scp'
    }
}

task deploy(dependsOn: 'distTar')  {

    doLast {
        def fileName = "${distributions.main.baseName}.tgz";

        ssh.run {
            session(remotes.prod) {
                put from: "$buildDir/distributions/$fileName", into: '~/';

                execute "tar -zxf $fileName"
            }
        }
    }
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "io.dropwizard:dropwizard-core:1.0.2";
    compile "io.dropwizard:dropwizard-db:1.0.2";
    compile "io.dropwizard:dropwizard-assets:1.0.2";
    compile "io.dropwizard:dropwizard-client:1.0.2";
    compile "io.progix.dropwizard:dropwizard-jooq:0.2.0";

    compile project(':util')
    compile project(':database')
    compile project(':etl')
    compile project(':search')
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.justweighit.Application'
        )
    }
}

mainClassName = "com.justweighit.Application"

run {
    args 'server', "$rootProject.projectDir/backend/src/main/resources/config.yml"
}

task 'import-all'(type: RunTask, dependsOn: 'compileJava') {
    main = 'com.justweighit.Application'
    args 'import-all', "$rootProject.projectDir/backend/src/main/resources/config.yml"
}

public class RunTask extends JavaExec {

    @TaskAction
    @Override
    public void exec() {
        classpath = project.sourceSets.main.runtimeClasspath

        if (project.hasProperty('args')) {
            args project.args.split('\\s')
        }

        super.exec();
    }
}