
buildscript {
    ext.kotlin_version = '1.4.10'

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.hidetake:gradle-ssh-plugin:2.7.0'
    }
}

plugins {
    id 'org.hidetake.ssh' version '2.7.0'
}

apply plugin: 'application'
apply plugin: 'kotlin'
apply plugin: 'distribution'
apply plugin: 'org.hidetake.ssh'

distributions {
    main {
        distributionBaseName = 'justweighit'
        contents {
            from ('src/main/resources/config.yml')
            from ('../frontend/dist') {
                into 'frontend'
            }
        }
    }
}

distTar {
    compression = Compression.GZIP
}

remotes {
    prod {
        host = 'justweighit.com'
        user = 'jwi'
        identity = file(project.property('SSH_KEY_FILE'))
        fileTransfer = 'scp'
    }
}

task deploy(dependsOn: 'distTar')  {

    doLast {
        def fileName = "${distributions.main.baseName}.tgz";

        ssh.run {
            session(remotes.prod) {
                put from: "$buildDir/distributions/$fileName", into: '~/';

                execute "tar -zxf $fileName"
            }
        }
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "io.dropwizard:dropwizard-core:2.0.0";
    implementation "io.dropwizard:dropwizard-db:2.0.0";
    implementation "io.dropwizard:dropwizard-assets:2.0.0";
    implementation "io.dropwizard:dropwizard-client:2.0.0";

    implementation project(':util')
    implementation project(':database')
    implementation project(':etl')
    compile project(':search')
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.justweighit.Application'
        )
    }
}

mainClassName = "com.justweighit.Application"

run {
    args 'server', "$rootProject.projectDir/backend/src/main/resources/config.yml"
}

task 'import-all'(type: RunTask, dependsOn: 'compileJava') {
    main = 'com.justweighit.Application'
    args 'import-all', "$rootProject.projectDir/backend/src/main/resources/config.yml"
}

public class RunTask extends JavaExec {

    @TaskAction
    @Override
    public void exec() {
        classpath = project.sourceSets.main.runtimeClasspath

        if (project.hasProperty('args')) {
            args project.args.split('\\s')
        }

        super.exec();
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "12"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "12"
    }
}